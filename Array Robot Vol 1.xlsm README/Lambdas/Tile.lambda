/*
Tile the outputs of a single-parameter function given an array map of parameters.  (params - array of parameters arranged how function results to be tiled; function - single-parameter Lambda name or function)
*/
Tile =LAMBDA(params,function,LET(    \\LambdaName, "TILE",    \\CommandName, "Tile",    \\Description, "Tile the outputs of a single-parameter function given an array map of parameters.",    \\Parameters, {"params","array of parameters arranged how function results to be tiled";"function","single-parameter Lambda name or function"},    \\Source, "Written by @ExcelRobot but inspired by Owen Price's STACKER lambda.",
   firstrow, function(INDEX(params,1,1)),
   stacker, LAMBDA(stack,param,VSTACK(stack,function(param))),
   firstcol, IF(ROWS(params)=1,firstrow,REDUCE(firstrow,DROP(TAKE(params,,1),1),stacker)),
   Result, IF(COLUMNS(params)=1,firstcol,HSTACK(firstcol,Tile(DROP(params,,1),function))),
   Result
));